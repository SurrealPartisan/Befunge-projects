>                                                                    v
v"Guess the number I have in mind! It's between 0 and 80, inclusive!"<

>:#,_v
     2
     5                                               >:#,_25*,@
     *       Memory cells: [ , ]                     ^"Correct!"<
     ,                                                           
          The block 
          below is a
          base-9 RNG
     v                                                           
 v <   <                                                         
   0   3            >       74*6p  >    & 32*5*6p 74*6g32*5*6g-!|
 v1? ? ?4v      >   +              ^             <               
   2   5        4   ^                   >:#,_25*,^               
   v6?7v      v3?5> ^                   ^"Bigger!"<              
     8            7 ^                             |`g6*5*23g6*47<
 >>>>>>>>>>9*>  ? ?8^                  v"Smaller!"<
                  6 ^                  >:#,_25*,v
              v0?2> ^              ^            <
                1   ^
              > >   ^

